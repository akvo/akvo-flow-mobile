apply plugin: 'java'
apply plugin: 'application'

mainClassName = 'org.akvo.flow.deploy.Deploy'

repositories {
    mavenCentral()
}

Properties versionProps = loadProperties('version.properties')

//create a single Jar with all dependencies
task fatJar(type: Jar) {
    manifest {
        attributes 'Main-Class': 'org.akvo.flow.deploy.Deploy'
    }
    baseName = 'deploy'
    version = versionProps['VERSION'].toString()
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

sourceCompatibility = 1.7
targetCompatibility = 1.7

dependencies {
    compile "com.google.appengine:appengine-api-1.0-sdk:1.9.30"
    compile "com.google.appengine:appengine-remote-api:1.9.30"
    compile "com.amazonaws:aws-java-sdk:1.7.1"
    compile "commons-codec:commons-codec:1.9"
    compile "commons-logging:commons-logging:1.1.3"
    compile "org.apache.httpcomponents:httpclient:4.3.2"
    compile "org.apache.httpcomponents:httpcore:4.3.2"
    compile "com.fasterxml.jackson.core:jackson-annotations:2.3.0"
    compile "com.fasterxml.jackson.core:jackson-core:2.3.0"
    compile "com.fasterxml.jackson.core:jackson-databind:2.3.0"
}

private Properties loadProperties(String fileName) {
    Properties versionProps = new Properties()
    def versionPropsFile = file(fileName)
    if (versionPropsFile.canRead()) {
        versionProps.load(new FileInputStream(versionPropsFile))
    } else {
        throw new GradleException("Could not read ${fileName}")
    }
    versionProps
}
